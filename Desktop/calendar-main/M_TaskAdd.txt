Option Explicit

Public Sub AddTask_Click()
    Dim wsTaskAdd As Worksheet
    Dim wsCalendar As Worksheet
    Dim wsSubmission As Worksheet

    Dim taskName As String, 担当者 As String, 依頼日Str As String, 〆切日Str As String, 地整〆切日Str As String
    Dim newRowCal As Long
    Dim dataStartRowCal As Long

    dataStartRowCal = CALENDAR_HEADER_ROWS + 1

    Set wsTaskAdd = GetSheet(TASK_ADD_SHEET_NAME)
    Set wsCalendar = GetSheet(CALENDAR_SHEET_NAME)
    Set wsSubmission = GetSheet(SUBMISSION_SHEET_NAME)

    If wsTaskAdd Is Nothing Or wsCalendar Is Nothing Or wsSubmission Is Nothing Then
        ShowError "必要なシートが見つかりません。" & vbCrLf & _
                  IIf(wsTaskAdd Is Nothing, TASK_ADD_SHEET_NAME & "なし ", "") & _
                  IIf(wsCalendar Is Nothing, CALENDAR_SHEET_NAME & "なし ", "") & _
                  IIf(wsSubmission Is Nothing, SUBMISSION_SHEET_NAME & "なし", "")
        Exit Sub
    End If

    taskName = Trim(wsTaskAdd.Cells(2, "B").Value)
    担当者 = Trim(wsTaskAdd.Cells(3, "B").Value)
    依頼日Str = Trim(wsTaskAdd.Cells(4, "B").Value)
    〆切日Str = Trim(wsTaskAdd.Cells(5, "B").Value)
    地整〆切日Str = Trim(wsTaskAdd.Cells(6, "B").Value)

    If Not ValidateTaskInput(taskName, 担当者, 依頼日Str, 〆切日Str, 地整〆切日Str, wsCalendar) Then Exit Sub

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    newRowCal = GetLastDataRow(wsCalendar, "C")
    If newRowCal < dataStartRowCal Then
        newRowCal = dataStartRowCal
    ElseIf wsCalendar.Cells(newRowCal, "C").Value <> "" Then
        newRowCal = newRowCal + 1
    End If
    If newRowCal = 0 And dataStartRowCal > 0 Then newRowCal = dataStartRowCal


    With wsCalendar
        .Cells(newRowCal, "C").Value = taskName
        .Cells(newRowCal, "D").Value = 担当者
        .Cells(newRowCal, "E").Value = CDate(依頼日Str)
        .Cells(newRowCal, "E").NumberFormat = "m/d"
        .Cells(newRowCal, "F").Value = CDate(〆切日Str)
        .Cells(newRowCal, "F").NumberFormat = "m/d"
        If 地整〆切日Str <> "-" And 地整〆切日Str <> "" Then
            .Cells(newRowCal, "G").Value = CDate(地整〆切日Str)
            .Cells(newRowCal, "G").NumberFormat = "m/d"
        Else
            .Cells(newRowCal, "G").Value = "-"
        End If
        Dim btnDel As Button, btnSub As Button
        Set btnDel = CreateButton(wsCalendar, .Cells(newRowCal, "A"), "×", "M_Calendar.DeleteTask_FromButton", , .Rows(newRowCal).Height * 0.9, 8)
        Set btnSub = CreateButton(wsCalendar, .Cells(newRowCal, "H"), "表示", "M_Calendar.ShowTaskSubmissionStatus_FromButton", , .Rows(newRowCal).Height * 0.9, 8)
        If btnDel Is Nothing Or btnSub Is Nothing Then
            ShowError "タスク行へのボタン作成に失敗しました。"
        End If
    End With

    Dim nextDataCol As Long
    Dim c As Long
    Dim headerRowForTaskName As Long
    headerRowForTaskName = MUNILIST_HEADER_ROW - 2

    nextDataCol = 0
    For c = Columns("F").Column To Columns("IV").Column Step 2
        If Trim(CStr(wsSubmission.Cells(headerRowForTaskName, c).Value)) = "" Then
            nextDataCol = c
            Exit For
        End If
    Next c

    If nextDataCol = 0 Then
        ShowError "提出状況管理シートに新しいタスクを追加する列スペースがありません。（F列?IV列が全て使用中）"
        GoTo CleanUpAndExit
    End If

    With wsSubmission
        .Cells(headerRowForTaskName, nextDataCol).Value = taskName
        .Cells(MUNILIST_HEADER_ROW, nextDataCol).Value = "提出日"
        .Cells(MUNILIST_HEADER_ROW, nextDataCol + 1).Value = "提出状況"
        With .Range(.Cells(MUNILIST_HEADER_ROW, nextDataCol), .Cells(MUNILIST_HEADER_ROW, nextDataCol + 1))
            .HorizontalAlignment = xlCenter
            .Font.Bold = True
        End With

        Dim muniListLastRow As Long
        muniListLastRow = GetLastDataRow(wsSubmission, "B")
        If muniListLastRow < MUNILIST_HEADER_ROW + 1 And gMuniListInitialized Then ' REM MODIFIED: フラグ確認
             muniListLastRow = MUNILIST_HEADER_ROW + UBound(gMuniList) - LBound(gMuniList)
        End If

        If muniListLastRow >= MUNILIST_HEADER_ROW + 1 Then
            Dim r As Long
            For r = MUNILIST_HEADER_ROW + 1 To muniListLastRow
                 If Trim(CStr(wsSubmission.Cells(r, "B").Value)) <> "" Or Trim(CStr(wsSubmission.Cells(r, "C").Value)) <> "" Then
                    .Cells(r, nextDataCol).NumberFormat = "m/d"
                    .Cells(r, nextDataCol + 1).Value = "×"
                    .Cells(r, nextDataCol + 1).HorizontalAlignment = xlCenter
                 End If
            Next r
        End If
        .Columns(nextDataCol).ColumnWidth = 10
        .Columns(nextDataCol + 1).ColumnWidth = 10
        .Columns(nextDataCol).Hidden = False
        .Columns(nextDataCol + 1).Hidden = False
    End With

    Call M_Calendar.DrawCalendar

CleanUpAndExit:
    Application.EnableEvents = True
    Application.ScreenUpdating = True
    If nextDataCol <> 0 Then
        ShowMessage "タスク「" & taskName & "」を追加しました。"
    End If
    
    Dim wsCal As Worksheet
    Set wsCal = GetSheet(CALENDAR_SHEET_NAME)
    If Not wsCal Is Nothing Then wsCal.Activate
End Sub

Private Function ValidateTaskInput(taskName As String, 担当者 As String, 依頼日Str As String, 〆切日Str As String, 地整〆切日Str As String, wsCalendar As Worksheet) As Boolean
    Dim msg As String
    Dim tempDate1 As Date, tempDate2 As Date
    Dim dataStartRowCal As Long
    dataStartRowCal = CALENDAR_HEADER_ROWS + 1

    ValidateTaskInput = False

    If wsCalendar Is Nothing Then
        ShowError "ValidateTaskInput: カレンダーシートが見つかりません。"
        Exit Function
    End If

    If taskName = "" Then msg = msg & "・調査物名" & vbCrLf
    If 担当者 = "" Then msg = msg & "・担当者名" & vbCrLf
    If 依頼日Str = "" Then msg = msg & "・県_依頼日" & vbCrLf
    If 〆切日Str = "" Then msg = msg & "・県_〆切日" & vbCrLf
    If msg <> "" Then
        ShowError "必須項目未入力：" & vbCrLf & msg
        Exit Function
    End If

    If Not IsDate(依頼日Str) Then msg = msg & "・県_依頼日の形式不正。" & vbCrLf
    If Not IsDate(〆切日Str) Then msg = msg & "・県_〆切日の形式不正。" & vbCrLf
    If 地整〆切日Str <> "-" And 地整〆切日Str <> "" And Not IsDate(地整〆切日Str) Then
        msg = msg & "・地整_〆切日の形式不正。" & vbCrLf
    End If
    If msg <> "" Then
        ShowError msg
        Exit Function
    End If

    tempDate1 = CDate(依頼日Str)
    tempDate2 = CDate(〆切日Str)
    If tempDate2 < tempDate1 Then
        ShowError "県_〆切日は県_依頼日以降にしてください。"
        Exit Function
    End If

    If FindTaskRow(taskName, wsCalendar) > 0 Then
        ShowError "調査物名「" & taskName & "」は重複しています。"
        Exit Function
    End If
    
    ValidateTaskInput = True
End Function


